// lunar lander game but ported to use the vector engine
CONST KEY_UP = 2, KEY_DOWN = 12, KEY_LEFT = 7, KEY_RIGHT = 8, KEY_ESC = 4;
CONST SCREEN_WIDTH = 320, SCREEN_HEIGHT = 240;
CONST FOREGROUND_COLOUR = RGB(0, 255, 0), BACKGROUND_COLOUR = RGB(0, 0, 0);
CONST FPS_CAP = 30;

EXPORT ENGINE_LANDER()
BEGIN
 LOCAL lander_pos = {0, 0};
 LOCAL lander_speed = {0, 0};
 LOCAL lander_angle = 0;
 LOCAL lander_spin_rate = 0;
 LOCAL gravity = 5;
 LOCAL spin_damp_rate = 0.97;
 LOCAL air_damp = 0.98;
 LOCAL thrust = 15;
 LOCAL torque = 10;
 LOCAL lander_size = {20, 8, 5};
 LOCAL fuel = 100, depletion_rate = 10;
 LOCAL zoom = 1, pan = {0, 0}, pan_rate = 0.2;
 LOCAL lander_vertices = {{-8, 0}, {0, -20}, {8, 0}}, exhaust_vertices = {{-8, 0}, {0, 6}, {8, 0}};
 LOCAL running = true;
 LOCAL terrain_n_vertices = 50, terrain_x_var = 30, terrain_y_var = 30, flat_length = 30;
 LOCAL terrain = GENERATE_TERRAIN(terrain_n_vertices-1, terrain_x_var, terrain_y_var, flat_length), terrain_on_screen;
 LOCAL frame_ticks, prev_ticks = TICKS()-1, fps, dt;

 WHILE running DO
  frame_ticks := TICKS() - prev_ticks;
  prev_ticks := TICKS();
  fps := 1000 / frame_ticks;
  IF fps > FPS_CAP THEN
   dt := 1000/FPS_CAP-frame_ticks;
   WAIT(dt/1000); 
  END;  

  IF ISKEYDOWN(KEY_UP) and fuel > 0 THEN
   lander_speed := lander_speed + ROTATE_VECTOR({0, -thrust / fps}, lander_angle);
   fuel := fuel - depletion_rate / fps;
  END;
  IF ISKEYDOWN(KEY_LEFT) THEN
   lander_spin_rate := lander_spin_rate - torque;
  END;
  IF ISKEYDOWN(KEY_RIGHT) THEN
   lander_spin_rate := lander_spin_rate + torque;
  END;
  IF ISKEYDOWN(KEY_ESC) THEN
   running := false;
  END;
  
  pan := pan + (({SCREEN_WIDTH/2/zoom, SCREEN_HEIGHT/3/zoom} - lander_pos) - pan) * pan_rate;
  
  lander_spin_rate := lander_spin_rate * spin_damp_rate;
  lander_angle := lander_angle + lander_spin_rate / fps;
  lander_speed[2] := lander_speed[2] + gravity / fps;
  lander_speed := lander_speed - lander_speed*air_damp/fps;
  lander_pos := lander_pos + lander_speed;
  
  RECT(BACKGROUND_COLOUR);
  TEXTOUT_P("FUEL " +STRING(ROUND(fuel, 0)), 0, 0, 2, FOREGROUND_COLOUR);
  TEXTOUT_P(STRING(ROUND(fps, 0)) + " FPS", SCREEN_WIDTH-40, 0, 2, FOREGROUND_COLOUR);
  
  DRAW_POLYGON(lander_vertices, lander_pos+pan, lander_angle, zoom, FOREGROUND_COLOUR);
  IF ISKEYDOWN(KEY_UP) and fuel > 0 THEN
   DRAW_POLYGON({exhaust_vertices[1], exhaust_vertices[2]+{RANDINT(-1, 1), RANDINT(0, 4)}, exhaust_vertices[3]}, lander_pos+pan, lander_angle, zoom, FOREGROUND_COLOUR);
  END;
  
  terrain_on_screen := GET_SEGMENTS_ON_SCREEN(terrain, pan, zoom, SCREEN_WIDTH);
  DRAW_TERRAIN(terrain_on_screen, {0, 100}+pan, zoom, FOREGROUND_COLOUR);
  IF CHECK_FOR_COLLISIONS(lander_vertices, lander_angle, lander_pos, terrain_on_screen) == 1 THEN
   WAIT(1);
   running := false;
  END;
 END;
 KILL;
END;