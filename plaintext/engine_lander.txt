// lunar lander game but ported to use the vector engine
CONST KEY_UP = 2, KEY_DOWN = 12, KEY_LEFT = 7, KEY_RIGHT = 8, KEY_ESC = 4;
CONST SCREEN_WIDTH = 320, SCREEN_HEIGHT = 240;
CONST FOREGROUND_COLOUR = RGB(0, 255, 0), BACKGROUND_COLOUR = RGB(0, 0, 0);
CONST REFRESH_RATE = 30;

EXPORT ENGINE_LANDER()
BEGIN
 LOCAL lander_pos = {0, 0};
 LOCAL lander_speed = {0, 0};
 LOCAL lander_angle = 0;
 LOCAL lander_spin_rate = 0;
 LOCAL gravity = 5;
 LOCAL spin_damp_rate = 0.97;
 LOCAL horizontal_damp = 0.98;
 LOCAL thrust = 15;
 LOCAL torque = 10;
 LOCAL lander_size = {20, 8, 5};
 LOCAL fuel = 1000, depletion_rate = 20;
 LOCAL zoom = 1, pan = {0, 0};
 LOCAL lander_vertices = {{-8, 0}, {0, -20}, {8, 0}}, exhaust_vertices = {{-8, 0}, {0, 6}, {8, 0}};
 LOCAL running = true;
 LOCAL terrain_n_vertices = 100, terrain_x_var = 30, terrain_y_var = 30, flat_length = 30;
 LOCAL terrain = GENERATE_TERRAIN(terrain_n_vertices-1, terrain_x_var, terrain_y_var, flat_length);
 
 WHILE running DO
  IF ISKEYDOWN(KEY_UP) and fuel > 0 THEN
   lander_speed := VECTOR_SUM(lander_speed, ROTATE_VECTOR({0, -thrust/REFRESH_RATE}, lander_angle));
   fuel := fuel - depletion_rate / REFRESH_RATE;
  END;
  IF ISKEYDOWN(KEY_LEFT) THEN
   lander_spin_rate := lander_spin_rate - torque/REFRESH_RATE;
  END;
  IF ISKEYDOWN(KEY_RIGHT) THEN
   lander_spin_rate := lander_spin_rate + torque/REFRESH_RATE;
  END;
  IF ISKEYDOWN(KEY_ESC) THEN
   running := false;
  END;

  // TODO: make panning better
  pan := {−lander_pos[1]/zoom+SCREEN_WIDTH/2/zoom, −lander_pos[2]/zoom+SCREEN_HEIGHT/3/zoom};
  
  lander_spin_rate := lander_spin_rate * spin_damp_rate;
  lander_angle := lander_angle + lander_spin_rate;
  lander_speed[1] := lander_speed[1] * horizontal_damp;
  lander_speed[2] := lander_speed[2] + gravity/REFRESH_RATE;
  lander_pos := VECTOR_SUM(lander_pos, lander_speed);

  RECT(BACKGROUND_COLOUR);
  TEXTOUT_P(STRING(ROUND(fuel, 0)), 0, 0, 2, FOREGROUND_COLOUR);

  DRAW_POLYGON(lander_vertices, VECTOR_SUM(lander_pos, pan), lander_angle, zoom, FOREGROUND_COLOUR);
  IF ISKEYDOWN(KEY_UP) and fuel > 0 THEN
   DRAW_POLYGON(exhaust_vertices, VECTOR_SUM(lander_pos, pan), lander_angle, zoom, FOREGROUND_COLOUR);
  END;

  DRAW_TERRAIN(terrain, VECTOR_SUM({0, 100}, pan), zoom, FOREGROUND_COLOUR, SCREEN_WIDTH);

  WAIT(1/REFRESH_RATE);
 END;
 KILL;
END;